// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BlueJayBird

//@version=4
study(title="[bjb] My Toolbox", shorttitle="myTx", overlay=true)

// CONTENT
// 1. Vertical Lines (every 1D and 4h)
// 2. Horizontal Lines (for HCL)

// READINGS:
// Extend lines into the future: https://www.pinecoders.com/faq_and_code/#how-can-i-draw-lines-or-labels-into-the-future
// Indicator repainting: https://www.tradingview.com/pine-script-docs/en/v4/essential/Indicator_repainting.html


// ========================================================================= //
// ----------------------------> Vertical Lines <--------------------------- //
// ========================================================================= //
// Displays vertical lines at the designated bar time(s).
// In this case, the selected timeframes are 4h and 1D
// TODO: Add automatization for timeframes larger than 4h and 1D, do not display those vertical lines at HTF.
// TODO: Add line for start of week.

// ------------------------------------------------------------------ Functions
vline(BarIndex, Color, LineStyle, LineWidth) => // Verticle Line Function, ≈50-54 lines maximum allowable per indicator
    // return = line.new(BarIndex,   0.0, BarIndex,     100.0, xloc.bar_index, extend.both, Color, LineStyle, LineWidth) // Suitable for study(overlay=false) and RSI, Stochastic, etc...
    // return = line.new(BarIndex,  -1.0, BarIndex,       1.0, xloc.bar_index, extend.both, Color, LineStyle, LineWidth) // Suitable for study(overlay=false) and +/-1.0 oscillators
    line.new(x1=BarIndex, y1=low-tr, x2=BarIndex, y2=high+tr, xloc=xloc.bar_index, extend=extend.both, color=Color, style=LineStyle, width=LineWidth) // Suitable for study(overlay=true)

// --------------------------------------------------------------------- Inputs
enable_line_1D   = input(defval=true, title="Enable 1D", type=input.bool, group="Vertical Lines")
enable_line_4h   = input(defval=true, title="Enable 4h", type=input.bool, group="Vertical Lines")
open_hour   = 00
open_minute = 00

// ----------------------------------------------------------------- Conditions
is_hour     = hour==open_hour
is_minute   = minute==open_minute
is_hour_4h 	= iff(hour==4 or hour==8 or hour==12 or hour==16 or hour==20, true, false)

// ---------------------------------------------------------- (pseudo) Plotting
// Colors
vl_1D_default_color =  color.new(color.yellow, 20)
vl_4h_default_color =  color.new(color.orange, 40)

// Variable assignment not required, not plotting
if(enable_line_1D and is_hour and is_minute)
    vline(bar_index, vl_1D_default_color, line.style_dashed, 2) 
    
if(enable_line_4h and is_hour_4h and is_minute)
    vline(bar_index, vl_4h_default_color, line.style_dotted, 2) 


// ========================================================================= //
// ------------------------------> Daily HLC <------------------------------ //
// ========================================================================= //
// High, Low, and Close, as horizontal lines from the selected resolution. Default is 1D.

// --------------------------------------------------------------------- Inputs
enable_daily_hlc   = input(defval=false, title="Enable Daily HLC", type=input.bool, group="Daily HLC")
hlc_resolution = input(defval="D", title="Resolution", type=input.resolution, group="Daily HLC")

high_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=high)
low_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=low)
close_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=close)

// ------------------------------------------------------------------ Variables
trim_line_time = '0000-0001'
trim_line_plot = time("1", trim_line_time)
var float high_security_value	= na
var float low_security_value	= na
var float close_security_value	= na

// ----------------------------------------------------------------- Conditions
if trim_line_plot[1]
    high_security_value		:= high_security
    low_security_value		:= low_security
    close_security_value	:= close_security

// ------------------------------------------------------------------- Plotting
plot(not trim_line_plot and enable_daily_hlc ? high_security_value : na, title="h", style=plot.style_linebr, linewidth=1, color=color.red)
plot(not trim_line_plot and enable_daily_hlc ? low_security_value : na, title="l", style=plot.style_linebr, linewidth=1, color=color.lime)
plot(not trim_line_plot and enable_daily_hlc ? close_security_value : na, title="c", style=plot.style_linebr, linewidth=1, color=color.orange)