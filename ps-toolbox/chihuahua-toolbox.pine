// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BlueJayBird

//@version=4
study(title="[bjb] Chihuahua Toolbox", shorttitle="chTx", overlay=true)

// TODO: Add supply and demand signals.

// CONTENT
// 1. Vertical Lines (every 1D and 4h)
// 2. Previous Daily HLC 
// 3. Classic EMAs
// 4. RSI Signals

// READINGS:
// Extend lines into the future: https://www.pinecoders.com/faq_and_code/#how-can-i-draw-lines-or-labels-into-the-future
// Indicator repainting: https://www.tradingview.com/pine-script-docs/en/v4/essential/Indicator_repainting.html


// ========================================================================= //
// ----------------------------> Vertical Lines <--------------------------- //
// ========================================================================= //
// Displays vertical lines at the designated bar time(s).
// In this case, the selected timeframes are 4h and 1D
// TODO: Add automatization for timeframes larger than 4h and 1D, do not display those vertical lines at HTF.

// ------------------------------------------------------------------ Functions
vline(BarIndex, Color, LineStyle, LineWidth) => // Verticle Line Function, ≈50-54 lines maximum allowable per indicator
    // return = line.new(BarIndex,   0.0, BarIndex,     100.0, xloc.bar_index, extend.both, Color, LineStyle, LineWidth) // Suitable for study(overlay=false) and RSI, Stochastic, etc...
    // return = line.new(BarIndex,  -1.0, BarIndex,       1.0, xloc.bar_index, extend.both, Color, LineStyle, LineWidth) // Suitable for study(overlay=false) and +/-1.0 oscillators
    line.new(x1=BarIndex, y1=low-tr, x2=BarIndex, y2=high+tr, xloc=xloc.bar_index, extend=extend.both, color=Color, style=LineStyle, width=LineWidth) // Suitable for study(overlay=true)

// --------------------------------------------------------------------- Inputs
// Lower Timeframes
enable_lines_ltf  = input(defval=true, title="Lower Timeframes", type=input.bool, group="Enable Vertical Lines LTF")
enable_lines_1h   = input(defval=true, title="1h", type=input.bool, group="Enable Vertical Lines LTF")
enable_lines_4h   = input(defval=true, title="4h", type=input.bool, group="Enable Vertical Lines LTF")
// Higher Timeframes
enable_lines_htf  = input(defval=true, title="Higher Timeframes", type=input.bool, group="Enable Vertical Lines HTF")
enable_lines_1D   = input(defval=true, title="1D", type=input.bool, group="Enable Vertical Lines HTF")
enable_lines_1W   = input(defval=true, title="1W", type=input.bool, group="Enable Vertical Lines HTF")
enable_lines_1M   = input(defval=true, title="1M", type=input.bool, group="Enable Vertical Lines HTF")
open_hour   = 00
open_minute = 00

// ----------------------------------------------------------------- Conditions
// Time intervals
is_hour     = hour==open_hour
is_minute   = minute==open_minute
is_hour_4h 	= iff(hour==4 or hour==8 or hour==12 or hour==16 or hour==20, true, false)
// Timeframe visibility, for the given vertical line...
visibility_1h = iff(timeframe.period=="1" or timeframe.period=="3" or timeframe.period=="5" or timeframe.period=="15" or timeframe.period=="30" or timeframe.period=="45", true, false) // Gray lines. Visible at...
visibility_4h = iff(timeframe.period=="60" or timeframe.period=="120" or timeframe.period=="180" or timeframe.period=="240" or timeframe.period=="D" or timeframe.period=="W" or timeframe.period=="M", false, true) // Orange lines. Not visible at...
visibility_1D = iff(timeframe.period=="D" or timeframe.period=="W" or timeframe.period=="M", false, true) // Yellow lines. Not visible at...
visibility_1W = iff(timeframe.period=="W" or timeframe.period=="M", false, true) // Blue lines. Not visible at...

// ---------------------------------------------------------- (pseudo) Plotting
// Colors
vl_gray =  color.new(color.gray, 50) // For 1h
vl_orange =  color.new(color.orange, 40) // For 4h
vl_yellow =  color.new(color.yellow, 40) // For sessions
vl_green =  color.new(#4caf50, 40) // For beginning of week. Green.
vl_blue =  color.new(#2962ff, 40) // For beginning of month. Blue.

// Variable assignment not required, not plotting
if(enable_lines_htf and is_hour and is_minute)
    // TODO: How do I change the color line for 'start of week' and 'start of month'? At 1D vline.
    if enable_lines_1M and dayofmonth==1
        vline(bar_index, vl_blue, line.style_dashed, 2)
    else if enable_lines_1W and dayofweek==dayofweek.monday
        vline(bar_index, vl_green, line.style_dashed, 2)
    else if enable_lines_1D and visibility_1D
        vline(bar_index, vl_yellow, line.style_dashed, 2) 
    
if enable_lines_ltf
    if enable_lines_4h and is_hour_4h and is_minute and visibility_4h
        vline(bar_index, vl_orange, line.style_dotted, 2)
    else if enable_lines_1h and is_minute and visibility_1h
        vline(bar_index, vl_gray, line.style_dotted, 2)


// ========================================================================= //
// ---------------------> Previous Daily HLC <------------------------------ //
// ========================================================================= //
// High, Low, and Close, as horizontal lines from the selected resolution. From the previous period. Default is 1D.

// --------------------------------------------------------------------- Inputs
enable_previous_hlc   = input(defval=true, title="Enable Previous HLC", type=input.bool, group="Previous HLC Settings")
hlc_resolution = input(defval="D", title="Resolution", type=input.resolution, group="Previous HLC Settings")

high_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=high)
low_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=low)
close_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=close)

// ------------------------------------------------------------------ Variables
trim_line_time = '0000-0001'
trim_line_plot = time("1", trim_line_time)
var float high_security_value	= na
var float low_security_value	= na
var float close_security_value	= na

// ----------------------------------------------------------------- Conditions
if trim_line_plot[1]
    high_security_value		:= high_security
    low_security_value		:= low_security
    close_security_value	:= close_security

// ------------------------------------------------------------------- Plotting
plot(not trim_line_plot and enable_previous_hlc ? high_security_value : na, title="high", style=plot.style_linebr, linewidth=1, color=color.red)
plot(not trim_line_plot and enable_previous_hlc ? low_security_value : na, title="low", style=plot.style_linebr, linewidth=1, color=color.lime)
plot(not trim_line_plot and enable_previous_hlc ? close_security_value : na, title="close", style=plot.style_linebr, linewidth=1, color=color.orange)


// ========================================================================= //
// ---------------------------> Classic EMAs <------------------------------ //
// ========================================================================= //
// 3 Exponential Moving Averages, they are classic EMAs 20/50/200

// --------------------------------------------------------------------- Inputs
ema_enable = input(title="Enable EMAs 20/50/200", defval=true, type=input.bool, group="EMAs Settings")
ema_enable_fill_a_b = input(title="Enable EMAs Fill 20/50", defval=true, type=input.bool, group="EMAs Settings")
ema_enable_fill_b_c = input(title="Enable EMAs Fill 50/200", defval=false, type=input.bool, group="EMAs Settings")
ema_source = input(title="Source", defval=low, type=input.source, group="EMAs Settings")
ema_resolution = input(title="Resolution", defval="", type=input.resolution, group="EMAs Settings")

// ----------------------------------------------------------------- Securities
ema_security_20 = security(symbol=syminfo.tickerid, resolution=ema_resolution, expression=ema(ema_source, 20))
ema_security_50 = security(symbol=syminfo.tickerid, resolution=ema_resolution, expression=ema(ema_source, 50))
ema_security_200 = security(symbol=syminfo.tickerid, resolution=ema_resolution, expression=ema(ema_source, 200))

// --------------------------------------------------------------------- Colors
color_upwards_price = #26a69a // Classic Green
color_downwards_price = #ef5350 // Classic Red

// ------------------------------------------------------------------- Plotting
ema_a_plot = plot(series=ema_enable ? ema_security_20 : na, title="EMA20", color=color.aqua, linewidth=2, transp=30)
ema_b_plot = plot(series=ema_enable ? ema_security_50 : na, title="EMA50", color=color.orange, linewidth=2, transp=30)
ema_c_plot = plot(series=ema_enable ? ema_security_200 : na, title="EMA200", color=color.fuchsia, linewidth=2, transp=30)

// ----------------------------------------------------------------------- Fill
ema_fill_a_b = ema_enable_fill_a_b ? (ema_security_20 >= ema_security_50 ? color_upwards_price : color_downwards_price) : na
ema_fill_b_c = ema_enable_fill_b_c ? (ema_security_50 >= ema_security_200 ? color_upwards_price : color_downwards_price) : na
fill(plot1 = ema_a_plot, plot2 = ema_b_plot, color = ema_fill_a_b, transp = 80, title = "EMAs 20/50 Fill")
fill(plot1 = ema_b_plot, plot2 = ema_c_plot, color = ema_fill_b_c, transp = 80, title = "EMAs 50/200 Fill")


// ========================================================================= //
// ----------------------------> RSI Signals <------------------------------ //
// ========================================================================= //

//------------------------------------------------------------------- Constants
transparency_rsi_signals = 20
draw_ort = true // "Draw On Candles Yet To Close", on real time

// --------------------------------------------------------------------- Inputs
enable_rsi_signals = input(title="Enable", type=input.bool, defval=true, group="RSI Signlas")
source_rsi_signals =  input(defval=close, title="Source", group="RSI Signlas")
length_rsi_signals = input(defval=14, minval=1, title="Length", group="RSI Signlas")
oversold_rsi_signals = input(defval=30, minval=1, title="Oversold", group="RSI Signlas")
overbought_rsi_signals = input(defval=70, minval=1, title="Overbought", group="RSI Signlas")

rsi_security_resolution = input(title="Resolution", defval="", type=input.resolution)

// -------------------------------------------------- Calculations and Security
rsi_value = rsi(source_rsi_signals, length_rsi_signals)
rsi_security = security(symbol=syminfo.tickerid, resolution=rsi_security_resolution, expression=rsi_value)
rsi_os = rsi_security <= oversold_rsi_signals
rsi_ob = rsi_security >= overbought_rsi_signals

// ------------------------------------------------------------------- Plotting
plotshape(rsi_ob and enable_rsi_signals and (barstate.ishistory or draw_ort), color=color.yellow, transp=30, text = "", style=shape.triangledown, title="RSI Overbought", location=location.abovebar)
plotshape(rsi_os and enable_rsi_signals and (barstate.ishistory or draw_ort), color=color.yellow, transp=30, text = "", style=shape.triangleup, title = "RSI Oversold", location=location.belowbar)
