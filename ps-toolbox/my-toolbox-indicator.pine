// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BlueJayBird

//@version=4
study(title="[bjb] My Toolbox", shorttitle="myTx", overlay=true)

// CONTENT
// 1. Vertical Lines (every 1D and 4h)
// 2. Horizontal Lines (for HCL)

// READINGS:
// Extend lines into the future: https://www.pinecoders.com/faq_and_code/#how-can-i-draw-lines-or-labels-into-the-future
// Indicator repainting: https://www.tradingview.com/pine-script-docs/en/v4/essential/Indicator_repainting.html


// ========================================================================= //
// ----------------------------> Vertical Lines <--------------------------- //
// ========================================================================= //
// Displays vertical lines at the designated bar time(s).
// In this case, the selected timeframes are 4h and 1D
// TODO: Add automatization for timeframes larger than 4h and 1D, do not display those vertical lines at HTF.
// TODO: Add line for start of week.

// ------------------------------------------------------------------ Functions
vline(BarIndex, Color, LineStyle, LineWidth) => // Verticle Line Function, ≈50-54 lines maximum allowable per indicator
    // return = line.new(BarIndex,   0.0, BarIndex,     100.0, xloc.bar_index, extend.both, Color, LineStyle, LineWidth) // Suitable for study(overlay=false) and RSI, Stochastic, etc...
    // return = line.new(BarIndex,  -1.0, BarIndex,       1.0, xloc.bar_index, extend.both, Color, LineStyle, LineWidth) // Suitable for study(overlay=false) and +/-1.0 oscillators
    line.new(x1=BarIndex, y1=low-tr, x2=BarIndex, y2=high+tr, xloc=xloc.bar_index, extend=extend.both, color=Color, style=LineStyle, width=LineWidth) // Suitable for study(overlay=true)

// --------------------------------------------------------------------- Inputs
// Lower Timeframes
enable_lines_ltf  = input(defval=true, title="Lower Timeframes", type=input.bool, group="Enable Vertical Lines LTF")
enable_lines_1h   = input(defval=true, title="1h", type=input.bool, group="Enable Vertical Lines LTF")
enable_lines_4h   = input(defval=true, title="4h", type=input.bool, group="Enable Vertical Lines LTF")
// Higher Timeframes
enable_lines_htf  = input(defval=true, title="Higher Timeframes", type=input.bool, group="Enable Vertical Lines HTF")
enable_lines_1D   = input(defval=true, title="1D", type=input.bool, group="Enable Vertical Lines HTF")
enable_lines_1W   = input(defval=true, title="1W", type=input.bool, group="Enable Vertical Lines HTF")
enable_lines_1M   = input(defval=true, title="1M", type=input.bool, group="Enable Vertical Lines HTF")
open_hour   = 00
open_minute = 00

// ----------------------------------------------------------------- Conditions
// Time intervals
is_hour     = hour==open_hour
is_minute   = minute==open_minute
is_hour_4h 	= iff(hour==4 or hour==8 or hour==12 or hour==16 or hour==20, true, false)
// Timeframe visibility, if given timeframe is NOT less than...
visibility_1h = iff(timeframe.period=="60", false, true)
visibility_4h = iff(timeframe.period=="60" or timeframe.period=="240" or timeframe.period=="D" or timeframe.period=="W", false, true)
visibility_1D = iff(timeframe.period=="D" or timeframe.period=="W" or timeframe.period=="M", false, true)
visibility_1W = iff(timeframe.period=="W" or timeframe.period=="M", false, true)

// ---------------------------------------------------------- (pseudo) Plotting
// Colors
vl_gray =  color.new(color.gray, 50) // For 1h
vl_orange =  color.new(color.orange, 40) // For 4h
vl_yellow =  color.new(color.yellow, 40) // For sessions
vl_green =  color.new(#4caf50, 40) // For beginning of week
vl_blue =  color.new(#2962ff, 40) // For beginning of month

// Variable assignment not required, not plotting
if(enable_lines_htf and is_hour and is_minute)
    // TODO: How do I change the color line for 'start of week' and 'start of month'? At 1D vline.
    if enable_lines_1M and dayofmonth==1
        vline(bar_index, vl_blue, line.style_dashed, 2)
    else if enable_lines_1W and dayofweek==dayofweek.monday
        vline(bar_index, vl_green, line.style_dashed, 2)
    else if enable_lines_1D and visibility_1D
        vline(bar_index, vl_yellow, line.style_dashed, 2) 
    
if enable_lines_ltf
    if enable_lines_4h and is_hour_4h and is_minute
        vline(bar_index, vl_orange, line.style_dotted, 2)
    else if enable_lines_1h and is_minute and visibility_1h
        vline(bar_index, vl_gray, line.style_dotted, 2)

// ========================================================================= //
// ------------------------------> Daily HLC <------------------------------ //
// ========================================================================= //
// High, Low, and Close, as horizontal lines from the selected resolution. Default is 1D.

// --------------------------------------------------------------------- Inputs
enable_daily_hlc   = input(defval=false, title="Enable Daily HLC", type=input.bool, group="Daily HLC")
hlc_resolution = input(defval="D", title="Resolution", type=input.resolution, group="Daily HLC")

high_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=high)
low_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=low)
close_security = security(symbol=syminfo.tickerid, resolution=hlc_resolution, expression=close)

// ------------------------------------------------------------------ Variables
trim_line_time = '0000-0001'
trim_line_plot = time("1", trim_line_time)
var float high_security_value	= na
var float low_security_value	= na
var float close_security_value	= na

// ----------------------------------------------------------------- Conditions
if trim_line_plot[1]
    high_security_value		:= high_security
    low_security_value		:= low_security
    close_security_value	:= close_security

// ------------------------------------------------------------------- Plotting
plot(not trim_line_plot and enable_daily_hlc ? high_security_value : na, title="h", style=plot.style_linebr, linewidth=1, color=color.red)
plot(not trim_line_plot and enable_daily_hlc ? low_security_value : na, title="l", style=plot.style_linebr, linewidth=1, color=color.lime)
plot(not trim_line_plot and enable_daily_hlc ? close_security_value : na, title="c", style=plot.style_linebr, linewidth=1, color=color.orange)