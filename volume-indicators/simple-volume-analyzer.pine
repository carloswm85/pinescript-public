// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © BlueJayBird
//@version=4

study(title="[bjb] SVA - Simple Volume Analyzer, by @BlueJayBird", shorttitle="SVA", format = format.volume, precision = 0, scale = scale.right, overlay = false)

// ================================================= //
// --------> SVA - Simple Volume Analyzer <--------- //
// ================================================= //

// ========================================================= Inputs
// Main inputs
volume_enable = input(defval = true, title = "Volume", type = input.bool, group="Enablers")
volume_enable_ma = input(defval = true, title = "Volume MAs (VMA)", type = input.bool, group="Enablers")

// Signals
enable_supply_signals = input(defval = false, title = "Supply Zone Signals (SZS)", type = input.bool, group="Enablers")
enable_demand_signals = input(defval = false, title = "Demand Zone Signals (DZS)", type = input.bool, group="Enablers")

// Multipliers 
volume_res1_multiplier = input(defval = 1.1, title = "Low Volume (LV)", type = input.float, group="Multipliers")
volume_res2_multiplier = input(defval = 1.5, title = "Medium Volume (MV)", type = input.float, group="Multipliers")
volume_res3_multiplier = input(defval = 2.1, title = "High Volume (HV)", type = input.float, group="Multipliers")
volume_res4_multiplier = input(defval = 7.5, title = "Ultra-High Volume (UHV)", type = input.float, group="Multipliers")
// Smoothers
volume_smoother_a = input(defval = 20, title = "VMA Sa (LV, MV, HV)", type = input.integer, group="Smoothers")
volume_smoother_b = input(defval = 5, title = "VMA Sb  (LV, MV, HV)", type = input.integer, group="Smoothers")
volume_smoother_c = input(defval = 200, title = "VMA Sc (UHV)", type = input.integer, group="Smoothers")
// (Sa) and (Sb) are smoother for LV, MV and HV MAs, they are used for modifying the color of the volume bars
// (Sc) is the smoother for the UHV, is less reactive than the others.
// I suggest you to keep the default values. I've extensively tested them.

// Miscellanea
volume_enable_reversed = input(defval = false, title = "Switch Down Bearish Volume Bars", type = input.bool, group="Enablers")
volume_resolution = ""
volume_sec = security(symbol = syminfo.tickerid, resolution = volume_resolution, expression = volume)
volume_sec_close = security(symbol = syminfo.tickerid, resolution = volume_resolution, expression = close)
volume_sec_open = security(symbol = syminfo.tickerid, resolution = volume_resolution, expression = open)
drawOnRealTime = true


// ========================================================= Colors
color_upwards_price = #26a69a // Classic Green
color_downwards_price = #ef5350 // Classic Red


// ========================================================= Functions
// Bar Colors
vol_color(ma, ma2, ma3, ma4) =>
    if volume_sec_close >= volume_sec_open
        // Bullish Volume
        if volume_sec >= ma4
            color.new(color.white, 0)
        else if volume_sec >= ma3
            color.new(color.yellow, 20)
        else if volume_sec >= ma2
            color.new(color_upwards_price, 10)
        else if volume_sec < ma2 and volume_sec >= ma
            color.new(color_upwards_price, 40)
        else
            vol_green = color.new(color_upwards_price, 70)
    else
        // Bearish
        if volume_sec >= ma4
            color.new(color.white, 0)
        else if volume_sec >= ma3
            color.new(color.yellow, 20)
        else if volume_sec >= ma2
            color.new(color_downwards_price, 10)
        else if volume_sec < ma2 and volume_sec >= ma
            color.new(color_downwards_price, 40)
        else
            color.new(color_downwards_price, 70)

// Text Colors
text_color(ma4) =>
    if volume_sec >= ma4
        color.new(color.black, 0)
    else
        color.new(color.white, 0)
        
// Label Colors
label_color(ma2, ma3, ma4) =>
    if volume_sec >= ma4
        color.new(color.white, 0)
    else if hl2 > hl2[1]
        color_downwards_price
    else
        color_upwards_price


// ========================================================= Calculations
vol_ma_resistance_1 = ema(sma(volume_sec * volume_res1_multiplier, volume_smoother_a), volume_smoother_b)
vol_ma_resistance_2 = ema(vwma(volume_sec * volume_res2_multiplier, volume_smoother_a), volume_smoother_b)
vol_ma_resistance_3 = ema(vwma(volume_sec * volume_res3_multiplier, volume_smoother_a), volume_smoother_b)
volume_res4_multiplier := timeframe.period == "60" ? 4.1 : 7.5
vol_ma_resistance_4 = sma(volume_sec * volume_res4_multiplier, volume_smoother_c)


// ========================================================= Plotting
// MA Colors
colorLV = color.new(#673ab7, 40)
colorMV = color.new(#9c27b0, 30)
colorHV = color.new(#e91e63, 50)
colorUHV = color.new(#ffffff, 50)
// MA (Volume Moving Averages) → Low, Mid, High, and Ultra-High Volumen
plot(series = volume_enable_ma and volume_resolution == "" ? vol_ma_resistance_4 : na, title = "UHV", color = colorUHV, linewidth = 1, style = plot.style_line, editable = true, display = display.none)
plot(series = volume_enable_ma and volume_resolution == "" ? vol_ma_resistance_3 : na, title = "HV", color = colorHV, linewidth = 1, style = plot.style_line, editable = true)
plot(series = volume_enable_ma and volume_resolution == "" ? vol_ma_resistance_2 : na, title = "MV", color = colorMV, linewidth = 1, style = plot.style_line, editable = true)
plot(series = volume_enable_ma and volume_resolution == "" ? vol_ma_resistance_1 : na, title = "LV", color = colorLV, linewidth = 1, style = plot.style_line, editable = true)

// Volume Bars
volume_data = volume_enable_reversed ? volume_sec_close >= volume_sec_open ? volume_sec : volume_sec * -1 : volume_sec // For switching bearish volume upside down from the 0 level
plot(series = volume_enable ? volume_data : na, title = "Vol", color = vol_color(vol_ma_resistance_1, vol_ma_resistance_2, vol_ma_resistance_3, vol_ma_resistance_4), linewidth = 3, style = plot.style_histogram, editable = true) // Main volume plotting, color is defined using the moving avarages as triggering color change levels

// Primary Signals (only)
shape_style = volume_enable_reversed ? shape.labelup : shape.labeldown
plotshape(enable_supply_signals and (barstate.ishistory or drawOnRealTime) and volume_sec >= vol_ma_resistance_2 and hl2 > hl2[1] ? volume_data + volume_data * 0.1 : na, color = label_color(vol_ma_resistance_2, vol_ma_resistance_3, vol_ma_resistance_4), style = shape_style, text = "S", textcolor = text_color(vol_ma_resistance_4), title = "Supply Zone Signals", location = location.absolute, editable = true)
plotshape(enable_demand_signals and (barstate.ishistory or drawOnRealTime) and volume_sec >= vol_ma_resistance_2 and hl2 <= hl2[1] ? volume_data + volume_data * 0.1 : na, color = label_color(vol_ma_resistance_2, vol_ma_resistance_3, vol_ma_resistance_4),style = shape_style, text = "D", textcolor = text_color(vol_ma_resistance_4), title = "Demand Zone Signals", location = location.absolute, editable = true)

// Zero Line (base line)
zero_line_color = color.new(color.silver, 50)
hline(price = 0, title = "Zero Line", color = zero_line_color, linestyle = hline.style_solid, linewidth = 1, editable = true)


// ========================================================= Custom Alarms (Send out an alert if this candle meets our conditions)
// TODO: Make new alarms, for diffent volumes? UHV and HV
// Alert Functions
sva_hv_alert(vol, ma3) =>
    iff (vol >= ma3, true, false)

sva_uhv_alert(vol, ma4) =>
    iff (vol >= ma4, true, false)

// Conditions
hv_alert    = sva_hv_alert  (volume_sec, vol_ma_resistance_3) 
uhv_alert   = sva_uhv_alert (volume_sec, vol_ma_resistance_4) 

// Conditional Alerts
alertcondition(hv_alert,    title="High Volume Alert!",         message="HV {{ticker}} | Vol: {{volume}} | TF: {{interval}} | Vol2: {{volume}}")
alertcondition(uhv_alert,   title="Ultra High Volume Alert!",   message="UHV for XXX")

// Alerts
if hv_alert
    // Trigger the alert the first time it occurs during the real-time bar.
    alert("Volume alert " + tostring(Volume) + " triggered.", alert.freq_once_per_bar)

plotchar(hv_alert, "hv", "▲", location.top, size = size.tiny)

// ========================================================= TODO:
// Demand Alarm
// Supply Alarm